/**
 * Represents a string of unicode characters.
 * Internally, this uses UTF-8, which is lazily converted to UTF-32 whenever the string needs to be processed using characters instead of bytes.
 */
immutable compound string
{
  #[]ubyte bytes;
  mutable ?#[]uint chars;

  /**
   * Creates an empty string.
   */
  this()
  {
    bytes = new [0]ubyte;
  }

  /**
   * Creates a string containing the specified UTF-8 bytes.
   * @param bytes - the UTF-8 bytes
   */
  this(#[]ubyte bytes)
  {
    this.bytes = bytes;
  }

  /**
   * Creates a string containing the specified unicode (UTF-32) characters.
   * @param chars - the unicode characters
   */
  this([]uint chars)
  {
    bytes = convertUTF32ToUTF8(chars);
    this.chars = chars;
  }

  /**
   * Creates a string containing the concatenation of the specified two strings.
   * @param str1 - the first string
   * @param str2 - the second string
   */
  this(string str1, string str2)
  {
    []ubyte bytes = new [str1.bytes.length + str2.bytes.length]ubyte;
    for (uint i = 0; i < str1.bytes.length; ++i)
    {
      bytes[i] = str1.bytes[i];
    }
    for (uint i = 0; i < str2.bytes.length; ++i)
    {
      bytes[str1.bytes.length + i] = str2.bytes[i];
    }
    this.bytes = bytes;
    if (str1.chars != null & str2.chars != null)
    {
      #[]uint chars1 = cast<#[]uint> str1.chars;
      #[]uint chars2 = cast<#[]uint> str2.chars;
      []uint chars = new [chars1.length + chars2.length]uint;
      for (uint i = 0; i < chars1.length; ++i)
      {
        chars[i] = chars1[i];
      }
      for (uint i = 0; i < chars2.length; ++i)
      {
        chars[chars1.length + i] = chars2[i];
      }
      this.chars = chars;
    }
  }

  /**
   * Creates a string containing the concatenation of the specified array of strings
   * @param strings - the array of strings to concatenate together
   */
  this([]string strings)
  {
    uint len = 0;
    ?uint charLength = 0;
    for (uint i = 0; i < strings.length; ++i)
    {
      len += strings[i].bytes.length;
      if charLength != null & strings[i].chars != null
      {
        charLength = (cast<uint> charLength) + (cast<uint> strings[i].chars?.length);
      }
      else
      {
        charLength = null;
      }
    }
    []ubyte bytes = new [len]ubyte;
    uint pos = 0;
    for (uint i = 0; i < strings.length; ++i)
    {
      for (uint j = 0; j < strings[i].bytes.length; ++j)
      {
        bytes[pos + j] = strings[i].bytes[j];
      }
      pos += strings[i].bytes.length;
    }
    this.bytes = bytes;
    if charLength != null
    {
      []uint chars = new [cast<uint> charLength]uint;
      uint charPos = 0;
      for (uint i = 0; i < strings.length; ++i)
      {
        #[]uint strChars = cast<#[]uint> strings[i].chars;
        for (uint j = 0; j < strChars.length; ++j)
        {
          chars[charPos + j] = strChars[j];
        }
        charPos += strChars.length;
      }
      this.chars = chars;
    }
  }

  /**
   * Finds the character length of this string, which is the number of unicode characters it contains.
   * This may not be equal to the number of bytes that represent this string, since in UTF-8 several bytes can represent a single character.
   * @return the number of unicode characters in this string
   */
  uint characterLength()
  {
    return getUnicodeChars().length;
  }

  /**
   * @return the number of bytes in the UTF-8 representation of this string.
   */
  uint byteLength()
  {
    return bytes.length;
  }

  /**
   * Finds the length of this string in terms of the number of characters it contains, rather than the number of bytes make up its UTF-8 representation.
   * The result is equivalent to that of characterLength().
   * @return the character length of this string.
   */
  uint length()
  {
    return characterLength();
  }

  /**
   * Finds the character at the specified position in this string, as a string.
   * @return the string representing the character at the specified index
   */
  string charAt(uint pos)
  {
    return substring(pos, pos + 1);
  }

  /**
   * Finds the character code at the specified position in this string, as a uint.
   * @return the uint code of the character at the specified position in this string
   */
  uint charCodeAt(uint pos)
  {
    return getUnicodeChars()[pos];
  }

  /**
   * Finds a substring of this string, starting at the specified offset, with the specified length.
   * @param off - the offset to the substring
   * @param len - the length of the substring
   * @return the substring of this string with the specified offset and length
   */
  string substring(uint start, uint end)
  {
    // TODO: make assertions about arguments
    uint len = end - start;
    #[]uint chars = getUnicodeChars();
    []uint subChars = new [len]uint;
    for (uint i = 0; i < len; ++i)
    {
      subChars[i] = chars[start + i];
    }
    return string(subChars);
  }

  /**
   * Finds the index of the specified string inside this one.
   * @param s - the string to find the index of
   * @return the index of the specified string inside this string, or null if it does not occur
   */
  ?uint indexOf(string s)
  {
    #[]uint chars = getUnicodeChars();
    #[]uint otherChars = getUnicodeChars();
    for (uint i = 0; i < chars.length - otherChars.length; ++i)
    {
      if chars[i] == otherChars[0]
      {
        for (uint j = 0; j < otherChars.length; ++j)
        {
          if chars[i + j] != otherChars[j]
          {
            // continue with the outer loop
            continue 2;
          }
        }
        return i;
      }
    }
    return null;
  }

  /**
   * @return the UTF-32 code points of the characters of this string.
   */
  #[]uint getUnicodeChars()
  {
    #[]uint chars = this.chars ?: convertUTF8ToUTF32(bytes);
    this.chars = chars;
    return chars;
  }

  /**
   * Checks whether the specified string is equal to this one.
   * @param str - the string to compare this one to
   * @return true iff the string objects represent the same sequence of characters
   */
  boolean equals(string str)
  {
    if (bytes.length != str.bytes.length)
    {
      return false;
    }
    for (uint i = 0; i < bytes.length; ++i)
    {
      if (bytes[i] != str.bytes[i])
      {
        return false;
      }
    }
    return true;
  }

  /**
   * @return this string
   */
  string toString()
  {
    return this;
  }

  /**
   * Finds the string which represents the specified boolean value.
   * @param value - the boolean value to find the string representation of
   * @return a string representation of value
   */
  static immutable string valueOf(boolean value)
  {
    return value ? "true" : "false";
  }

  /**
   * Finds the string which represents the specified signed value in base 10.
   * @param value - the signed value to find the string representation of
   * @return a string representation of value in base 10
   */
  static immutable string valueOf(long value)
  {
    return valueOf(value, 10);
  }

  /**
   * Finds the string which represents the specified unsigned value in base 10.
   * @param value - the unsigned value to find the string representation of
   * @return a string representation of value in base 10
   */
  static immutable string valueOf(ulong value)
  {
    return valueOf(value, 10);
  }

  /**
   * Finds the string which represents the specified signed value.
   * @param value - the signed value to find the string representation of
   * @param radix - the radix to encode the value with
   * @return a string representation of value in radix
   */
  static immutable string valueOf(long value, uint radix)
  {
    #[]ubyte numerals = "0123456789abcdefghijklmnopqrstuvwxyz".bytes;
    if radix < 2 | radix > numerals.length
    {
      return "#RadixError";
    }
    if value == 0
    {
      return "0";
    }
    uint length = 0;
    ulong abs = cast<ulong> value;
    if value < 0
    {
      ++length;
      abs = cast<ulong> -value;
      // because of the way negative numbers are represented,
      // even if value == long::MIN_VALUE (which cannot be negated),
      // abs will now be the correct unsigned value
    }
    ulong test = abs;
    while test != 0
    {
      test /= radix;
      ++length;
    }
    []ubyte bytes = new [length]ubyte;
    for (uint i = bytes.length - 1; i != 0; --i)
    {
      bytes[i] = numerals[cast<uint> (abs % radix)];
      abs /= radix;
    }
    if value < 0
    {
      bytes[0] = "-".bytes[0];
    }
    else
    {
      bytes[0] = numerals[cast<uint> abs];
    }
    return string(bytes);
  }

  /**
   * Finds the string which represents the specified unsigned value.
   * @param value - the unsigned value to find the string representation of
   * @param radix - the radix to encode the value with
   * @return a string representation of value in radix
   */
  static immutable string valueOf(ulong value, uint radix)
  {
    #[]ubyte numerals = "0123456789abcdefghijklmnopqrstuvwxyz".bytes;
    if radix < 2 | radix > numerals.length
    {
      return "#RadixError";
    }
    if value == 0
    {
      return "0";
    }
    uint length = 0;
    ulong test = value;
    while test != 0
    {
      test /= radix;
      ++length;
    }
    []ubyte bytes = new [length]ubyte;
    for (uint i = bytes.length - 1; i != 0; --i)
    {
      bytes[i] = numerals[cast<uint> (value % radix)];
      value /= radix;
    }
    bytes[0] = numerals[cast<uint> value];
    return string(bytes);
  }

  /**
   * Finds the string which represents the specified float.
   * @param value - the float value to find the string representation of
   * @return a string representation of value
   */
  static immutable string valueOf(float value)
  {
    uint binaryValue = maths::convertFloatBitsToUint(value);
    boolean negative = (binaryValue & 0b10000000000000000000000000000000) != 0;
    uint rawExponent = (binaryValue & 0b01111111100000000000000000000000) >> 23;
    uint mantissa    = (binaryValue & 0b00000000011111111111111111111111);
    if rawExponent == 0 & mantissa == 0
    {
      return negative ? "-0.0" : "0.0";
    }
    if rawExponent == 0b11111111
    {
      if mantissa == 0
      {
        return negative ? "-Infinity" : "Infinity";
      }
      return "NaN";
    }
    int exponent;
    if rawExponent == 0
    {
      exponent = -126;
    }
    else
    {
      exponent = cast<int> rawExponent - 127;
      mantissa |= 0b00000000100000000000000000000000;
    }

    (int, []ubyte) decimalExponent, digits = maths::findFloatingPointDigits(mantissa, 23, exponent - 23, -126 - 23, 9);
    boolean startWithOne = digits.length > 0 & digits[0] == 10;
    if startWithOne
    {
      digits[0] = 0;
    }
    return formatFloatingDigits(negative, decimalExponent, digits, startWithOne);
  }

  /**
   * Finds the string which represents the specified double.
   * @param value - the double value to find the string representation of
   * @return a string representation of value
   */
  static immutable string valueOf(double value)
  {
    ulong binaryValue = maths::convertDoubleBitsToUlong(value);
    boolean negative =  (binaryValue & 0b1000000000000000000000000000000000000000000000000000000000000000) != 0;
    ulong rawExponent = (binaryValue & 0b0111111111110000000000000000000000000000000000000000000000000000) >> 52;
    ulong mantissa   =  (binaryValue & 0b0000000000001111111111111111111111111111111111111111111111111111);
    if rawExponent == 0 & mantissa == 0
    {
      return negative ? "-0.0" : "0.0";
    }
    if rawExponent == 0b11111111111
    {
      if mantissa == 0
      {
        return negative ? "-Infinity" : "Infinity";
      }
      return "NaN";
    }
    int exponent;
    if rawExponent == 0
    {
      exponent = -1022;
    }
    else
    {
      exponent = cast<int> cast<uint> rawExponent - 1023;
      mantissa |= 0b0000000000010000000000000000000000000000000000000000000000000000;
    }

    (int, []ubyte) decimalExponent, digits = maths::findFloatingPointDigits(mantissa, 52, exponent - 52, -1022 - 52, 17);
    boolean startWithOne = digits.length > 0 & digits[0] == 10;
    if startWithOne
    {
      digits[0] = 0;
    }
    return formatFloatingDigits(negative, decimalExponent, digits, startWithOne);
  }

  /**
   * Finds the string which represents the specified array of floating point digits, in the format produced by maths::findFloatingPointDigits().
   * @param negative - true if the number is negative, false otherwise
   * @param decimalExponent - the decimal exponent to be applied to the digits
   * @param digits - the digits of the floating point number, in the format 0.[d0][d1][d2][d3][d4]...
   * @param startWithOne - true iff the digits array begins with a 10, implying that the number before the decimal point is a 1 instead of a 0
   * @return the string representation of the specified floating point digits
   */
  static immutable string formatFloatingDigits(boolean negative, int decimalExponent, []ubyte digits, boolean startWithOne)
  {
    ubyte zero = "0".bytes[0];
    if decimalExponent > 9 | decimalExponent < -6
    {
      if !startWithOne
      {
        --decimalExponent;
      }
      boolean digitsAfterPoint = digits.length > (startWithOne ? 0 : 1);
      []ubyte bytes = new [(startWithOne ? 1 : 0) + 1 + digits.length + (digitsAfterPoint ? 0 : 1)]ubyte;
      bytes[0] = startWithOne ? "1".bytes[0] : zero + digits[0];
      bytes[1] = ".".bytes[0];
      if !digitsAfterPoint
      {
        bytes[2] = zero;
      }
      uint offset = startWithOne ? 0 : 1;
      for (uint i = 0; i < digits.length - offset; ++i)
      {
        bytes[2 + i] = zero + digits[i + offset];
      }
      return (negative ? "-" : "") + string(bytes) + "E" + decimalExponent.toString();
    }
    if decimalExponent > 0
    {
      // if it starts with one, we need an extra digit for that one
      // if the whole number is in the integer part, we need to pad with zeros and add a zero after the decimal point
      uint length = (startWithOne ? 1 : 0) + 1 + (decimalExponent >= digits.length ? cast<uint> decimalExponent + 1 : digits.length);
      []ubyte bytes = new [length]ubyte;
      uint offset = 0;
      if startWithOne
      {
        bytes[0] = "1".bytes[0];
        offset = 1;
      }
      for (uint i = 0; i < digits.length & i < decimalExponent; ++i)
      {
        bytes[offset + i] = zero + digits[i];
      }
      if decimalExponent >= digits.length
      {
        // pad with zeros, and add ".0" to the end
        for (uint i = digits.length; i < decimalExponent; ++i)
        {
          bytes[offset + i] = zero;
        }
        bytes[offset + cast<uint> decimalExponent] = ".".bytes[0];
        bytes[offset + cast<uint> decimalExponent + 1] = zero;
        return (negative ? "-" : "") + string(bytes);
      }
      bytes[offset + cast<uint> decimalExponent] = ".".bytes[0];
      for (uint i = cast<uint> decimalExponent; i < digits.length; ++i)
      {
        bytes[offset + 1 + i] = zero + digits[i];
      }
      return (negative ? "-" : "") + string(bytes);
    }
    if decimalExponent == 0
    {
      []ubyte bytes = new [2 + digits.length]ubyte;
      bytes[0] = zero + (startWithOne ? 1 : 0);
      bytes[1] = ".".bytes[0];
      for (uint i = 0; i < digits.length; ++i)
      {
        bytes[2 + i] = zero + digits[i];
      }
      return (negative ? "-" : "") + string(bytes);
    }
    []ubyte bytes = new [2 + cast<uint> (-decimalExponent) + digits.length]ubyte;
    bytes[0] = zero;
    bytes[1] = ".".bytes[0];
    // pad with zeros
    for (uint i = 0; i < cast<uint> (-decimalExponent) - 1; ++i)
    {
      bytes[2 + i] = zero;
    }
    uint offset = 2 + cast<uint> (-decimalExponent);
    bytes[offset - 1] = zero + (startWithOne ? 1 : 0);
    for (uint i = 0; i < digits.length; ++i)
    {
      bytes[offset + i] = zero + digits[i];
    }
    return (negative ? "-" : "") + string(bytes);
  }

  /**
   * @param bytes - an array of UTF-8 bytes to the find the character length of
   * @return the number of characters represented by the specified UTF-8 bytes.
   */
  static immutable uint findUTF8Length(#[]ubyte bytes)
  {
    uint len = 0;
    for (uint i = 0; i < bytes.length; ++i)
    {
      if (bytes[i] >> 7) != 0
      {
        ubyte b = bytes[i];
        // skip the extra bytes
             if b >> 5 ==     0b110 { i += 1; }
        else if b >> 4 ==    0b1110 { i += 2; }
        else if b >> 3 ==   0b11110 { i += 3; }
        else if b >> 2 ==  0b111110 { i += 4; }
        else if b >> 1 == 0b1111110 { i += 5; }
      }
      ++len;
    }
    return len;
  }

  /**
   * @param bytes - an array of UTF-8 bytes to the find the UTF-32 representation of
   * @return the UTF-32 representation of the specified UTF-8 characters
   */
  static immutable []uint convertUTF8ToUTF32(#[]ubyte bytes)
  {
    uint len = findUTF8Length(bytes);
    []uint result = new [len]uint;
    uint pos = 0;
    for (uint i = 0; i < bytes.length; ++i)
    {
      ubyte b = bytes[i];
      if (b & 0b10000000) == 0
      {
        result[pos] = b;
      }
      else
      {
        ubyte b2 = (i+1 < bytes.length) ? bytes[i+1] : 0;
        if (b >> 5 == 0b110)
        {
          result[pos] = (cast<uint> (b  & 0x1f) << 6) |
                        (cast<uint> (b2 & 0x3f));
          i += 1;
        }
        else
        {
          ubyte b3 = (i+2 < bytes.length) ? bytes[i+2] : 0;
          if (b >> 4 == 0b1110)
          {
            result[pos] = (cast<uint> (b  & 0x0f) << 12) |
                          (cast<uint> (b2 & 0x3f) <<  6) |
                          (cast<uint> (b3 & 0x3f));
            i += 2;
          }
          else
          {
            ubyte b4 = (i+3 < bytes.length) ? bytes[i+3] : 0;
            if (b >> 3 == 0b11110)
            {
              result[pos] = (cast<uint> (b  & 0x07) << 18) |
                            (cast<uint> (b2 & 0x3f) << 12) |
                            (cast<uint> (b3 & 0x3f) <<  6) |
                            (cast<uint> (b4 & 0x3f));
              i += 3;
            }
            else
            {
              ubyte b5 = (i+4 < bytes.length) ? bytes[i+4] : 0;
              if (b >> 2 == 0b111110)
              {
                result[pos] = (cast<uint> (b  & 0x03) << 24) |
                              (cast<uint> (b2 & 0x3f) << 18) |
                              (cast<uint> (b3 & 0x3f) << 12) |
                              (cast<uint> (b4 & 0x3f) <<  6) |
                              (cast<uint> (b5 & 0x3f));
                i += 4;
              }
              else if (b >> 1 == 0b1111110)
              {
                ubyte b6 = (i+5 < bytes.length) ? bytes[i+5] : 0;
                result[pos] = (cast<uint> (b  & 0x01) << 30) |
                              (cast<uint> (b2 & 0x3f) << 24) |
                              (cast<uint> (b3 & 0x3f) << 18) |
                              (cast<uint> (b4 & 0x3f) << 12) |
                              (cast<uint> (b5 & 0x3f) <<  6) |
                              (cast<uint> (b6 & 0x3f));
                i += 5;
              }
            }
          }
        }
      }
      ++pos;
    }
    return result;
  }

  /**
   * @param chars - an array of UTF-32 character codes to the find the UTF-8 representation of
   * @return the UTF-8 representation of the specified UTF-32 character codes
   */
  static immutable []ubyte convertUTF32ToUTF8(#[]uint chars)
  {
    uint len = 0;
    for (uint i = 0; i < chars.length; ++i)
    {
           if (chars[i] & 0xffffff80) == 0 {len += 1;}
      else if (chars[i] & 0xfffff800) == 0 {len += 2;}
      else if (chars[i] & 0xffff0000) == 0 {len += 3;}
      else if (chars[i] & 0xffe00000) == 0 {len += 4;}
      else if (chars[i] & 0xfc000000) == 0 {len += 5;}
      else {len += 6;}
    }
    []ubyte bytes = new [len]ubyte;
    uint pos = 0;
    for (uint i = 0; i < chars.length; ++i)
    {
      if (chars[i] & 0xffffff80) == 0
      {
        bytes[pos] = cast<ubyte> chars[i];
        pos += 1;
      }
      else if (chars[i] & 0xfffff800) == 0
      {
        bytes[pos  ] = cast<ubyte> ((chars[i] >> 6) & 0x1f) | 0b11000000;
        bytes[pos+1] = cast<ubyte> ((chars[i]     ) & 0x3f) | 0b10000000;
        pos += 2;
      }
      else if (chars[i] & 0xffff0000) == 0
      {
        bytes[pos  ] = cast<ubyte> ((chars[i] >> 12) & 0x0f) | 0b11100000;
        bytes[pos+1] = cast<ubyte> ((chars[i] >>  6) & 0x3f) | 0b10000000;
        bytes[pos+2] = cast<ubyte> ((chars[i]      ) & 0x3f) | 0b10000000;
        pos += 3;
      }
      else if (chars[i] & 0xffe00000) == 0
      {
        bytes[pos  ] = cast<ubyte> ((chars[i] >> 18) & 0x07) | 0b11110000;
        bytes[pos+1] = cast<ubyte> ((chars[i] >> 12) & 0x3f) | 0b10000000;
        bytes[pos+2] = cast<ubyte> ((chars[i] >>  6) & 0x3f) | 0b10000000;
        bytes[pos+3] = cast<ubyte> ((chars[i]      ) & 0x3f) | 0b10000000;
        pos += 4;
      }
      else if (chars[i] & 0xfc000000) == 0
      {
        bytes[pos  ] = cast<ubyte> ((chars[i] >> 24) & 0x03) | 0b11111000;
        bytes[pos+1] = cast<ubyte> ((chars[i] >> 18) & 0x3f) | 0b10000000;
        bytes[pos+2] = cast<ubyte> ((chars[i] >> 12) & 0x3f) | 0b10000000;
        bytes[pos+3] = cast<ubyte> ((chars[i] >>  6) & 0x3f) | 0b10000000;
        bytes[pos+4] = cast<ubyte> ((chars[i]      ) & 0x3f) | 0b10000000;
        pos += 5;
      }
      else
      {
        bytes[pos  ] = cast<ubyte> ((chars[i] >> 30) & 0x01) | 0b11111100;
        bytes[pos+1] = cast<ubyte> ((chars[i] >> 24) & 0x3f) | 0b10000000;
        bytes[pos+2] = cast<ubyte> ((chars[i] >> 18) & 0x3f) | 0b10000000;
        bytes[pos+3] = cast<ubyte> ((chars[i] >> 12) & 0x3f) | 0b10000000;
        bytes[pos+4] = cast<ubyte> ((chars[i] >>  6) & 0x3f) | 0b10000000;
        bytes[pos+5] = cast<ubyte> ((chars[i]      ) & 0x3f) | 0b10000000;
        pos += 6;
      }
    }
    return bytes;
  }
}
